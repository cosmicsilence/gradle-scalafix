plugins {
    id 'org.ajoberstar.stutter' version '0.5.1'
    id 'org.ajoberstar.reckon' version '0.12.0'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'groovy'
    id 'java-gradle-plugin'
}

group 'io.github.cosmicsilence'

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'ch.epfl.scala:scalafix-cli_2.12.12:0.9.19'
    compatTestImplementation gradleTestKit()
    compatTestImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

stutter {
    sparse = true
    java(8) {
        compatibleRange '4.9', '5.0'
        compatibleRange '5.1'
    }
    java(11) {
        compatibleRange '4.9', '5.0'
        compatibleRange '5.1'
    }
}

validateTaskProperties {
    failOnWarning = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

reckon {
    scopeFromProp()
    stageFromProp('beta', 'rc', 'final')
}

pluginBundle {
    description = 'Gradle plugin for Scalafix'
    website = 'https://github.com/cosmicsilence/gradle-scalafix'
    vcsUrl = 'https://github.com/cosmicsilence/gradle-scalafix.git'
    plugins {
        scalafixPlugin {
            id = 'io.github.cosmicsilence.scalafix'
            displayName = 'Scalafix Plugin'
            tags = ['scalafix']
        }
    }
}

bintray {
    user = project.findProperty("bintray.user") ?: System.getenv("BINTRAY_USER")
    key  = project.findProperty("bintray.key")  ?: System.getenv("BINTRAY_KEY")
    pkg {
        repo = 'maven'
        name = 'gradle-scalafix'
        userOrg = 'cosmicsilence'
        version {
            name = project.version
        }
    }
    publications = ['pluginMaven']
}

def pomConfig = {
    licenses {
        license {
            name 'BSD 3-Clause License'
            url 'https://opensource.org/licenses/BSD-3-Clause'
        }
    }
    developers {
        developer {
            name 'Marcelo Cenerino'
            email 'marcelocenerine@gmail.com'
            organization 'Cosmic Silence'
            organizationUrl 'https://github.com/cosmicsilence'
        }
        developer {
            name 'Felix Sanjuan'
            email 'felix.sanjuan@icloud.com'
            organization 'Cosmic Silence'
            organizationUrl 'https://github.com/cosmicsilence'
        }
    }

    scm {
        url 'https://github.com/cosmicsilence/gradle-scalafix'
        connection 'git@github.com:cosmicsilence/gradle-scalafix.git'
        developerConnection 'git@github.com:cosmicsilence/gradle-scalafix.git'
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cosmicsilence/gradle-scalafix")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("github.key")  ?: System.getenv("GITHUB_KEY")
            }
        }
    }
    publications {
        pluginMaven(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Plugin for Scalafix')
                root.appendNode('description', 'This is a Gradle plugin for Scalafix, a refactoring and linting tool for Scala')
                root.appendNode('url', 'https://github.com/cosmicsilence/gradle-scalafix')
                root.children().last() + pomConfig
            }
        }
    }
}
