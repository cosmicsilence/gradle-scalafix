plugins {
    id 'java-gradle-plugin'
    id 'groovy'
    id 'org.ajoberstar.reckon' version '0.12.0'
    id 'maven-publish'
    id 'com.jfrog.bintray' version '1.8.4'
}

group 'io.github.cosmicsilence'

gradlePlugin {
    plugins {
        scalafix {
            id = 'io.github.cosmicsilence.scalafix'
            implementationClass = 'io.github.cosmicsilence.scalafix.ScalafixPlugin'
        }
    }
}

version '0.0.1-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    compile 'ch.epfl.scala:scalafix-interfaces:0.9.11'
    testImplementation gradleTestKit()
    testCompile 'junit:junit:4.12'
    testCompile 'org.spockframework:spock-core:1.3-groovy-2.5'

}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

reckon {
    scopeFromProp()
    stageFromProp('beta', 'rc', 'final')
}

bintray {
    user = project.findProperty("bintray.user") ?: System.getenv("BINTRAY_USER")
    key  = project.findProperty("bintray.key")  ?: System.getenv("BINTRAY_KEY")
    pkg {
        repo = 'maven'
        name = 'gradle-scalafix'
        userOrg = 'cosmicsilence'
        version {
            name = project.version
        }
    }
    publications = ['jar']
}

def pomConfig = {
    licenses {
        license {
            name 'BSD 3-Clause License'
            url 'https://opensource.org/licenses/BSD-3-Clause'
        }
    }
    developers {
        developer {
            name 'Marcelo Cenerino'
            email 'marcelocenerine@gmail.com'
            organization 'Cosmic Silence'
            organizationUrl 'https://github.com/cosmicsilence'
        }
        developer {
            name 'Felix Sanjuan'
            email 'fsanjuan@gmail.com'
            organization 'Cosmic Silence'
            organizationUrl 'https://github.com/cosmicsilence'
        }
    }

    scm {
        url 'https://github.com/cosmicsilence/gradle-scalafix'
        connection 'git@github.com:cosmicsilence/gradle-scalafix.git'
        developerConnection 'git@github.com:cosmicsilence/gradle-scalafix.git'
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cosmicsilence/gradle-scalafix")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_USER")
                password = project.findProperty("github.key")  ?: System.getenv("GITHUB_KEY")
            }
        }
    }
    publications {
        jar(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Plugin for Scalafix')
                root.appendNode('description', 'This is a Gradle plugin for Scalafix, a refactoring and linting tool for Scala')
                root.appendNode('url', 'https://github.com/cosmicsilence/gradle-scalafix')
                root.children().last() + pomConfig
            }
        }
    }
}