buildscript {
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'org.ajoberstar.stutter' version '0.7.3'
    id 'org.ajoberstar.reckon' version '0.18.1'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group 'io.github.cosmicsilence'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(8) // target version
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'ch.epfl.scala:scalafix-interfaces:0.14.2'
    compatTestImplementation gradleTestKit()
    compatTestImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
}

dependencyLocking {
    lockAllConfigurations()
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

stutter {
    sparse = true

    matrices {
        java8 {
            javaToolchain {
                languageVersion = JavaLanguageVersion.of(8)
            }
            gradleVersions {
                compatibleRange '4.10', '5.0'
                compatibleRange '5.1', '8.13-rc-1'
            }
        }

        java11 {
            javaToolchain {
                languageVersion = JavaLanguageVersion.of(11)
            }
            gradleVersions {
                compatibleRange '4.10', '5.0'
                compatibleRange '5.1', '8.13-rc-1'
            }
        }
    }
}

reckon {
    defaultInferredScope = 'patch'
    stages('beta', 'rc', 'final')
    scopeCalc = calcScopeFromProp().or(calcScopeFromCommitMessages())
    stageCalc = calcStageFromProp()
}

validatePlugins {
    failOnWarning = true
}

gradlePlugin {
    website = 'https://github.com/cosmicsilence/gradle-scalafix'
    vcsUrl = 'https://github.com/cosmicsilence/gradle-scalafix.git'

    plugins {
        scalafixPlugin {
            id = 'io.github.cosmicsilence.scalafix'
            displayName = 'Scalafix Plugin'
            description = 'Gradle plugin for Scalafix'
            tags = ['scalafix', 'scala']
            implementationClass = 'io.github.cosmicsilence.scalafix.ScalafixPlugin'
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cosmicsilence/gradle-scalafix")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("github.key")  ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        pluginMaven(MavenPublication) {
            pom {
                name = 'Gradle Plugin for Scalafix'
                description = 'Gradle plugin for Scalafix, a refactoring and linting tool for Scala'
                url = 'https://github.com/cosmicsilence/gradle-scalafix'
                licenses {
                    license {
                        name = 'BSD 3-Clause License'
                        url = 'https://opensource.org/licenses/BSD-3-Clause'
                    }
                }
                developers {
                    developer {
                        name = 'Marcelo Cenerino'
                        email = 'marcelocenerine@gmail.com'
                        organization = 'Cosmic Silence'
                        organizationUrl = 'https://github.com/cosmicsilence'
                    }
                    developer {
                        name = 'Felix Sanjuan'
                        email = 'felix.sanjuan@icloud.com'
                        organization = 'Cosmic Silence'
                        organizationUrl = 'https://github.com/cosmicsilence'
                    }
                }
                scm {
                    url = 'https://github.com/cosmicsilence/gradle-scalafix'
                    connection = 'git@github.com:cosmicsilence/gradle-scalafix.git'
                    developerConnection = 'git@github.com:cosmicsilence/gradle-scalafix.git'
                }
            }
        }
    }
}
