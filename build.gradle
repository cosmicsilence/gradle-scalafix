buildscript {
    configurations.classpath {
        resolutionStrategy.activateDependencyLocking()
    }
}

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'org.ajoberstar.stutter' version '0.7.3'
    id 'org.ajoberstar.reckon' version '0.18.1'
    id 'com.gradle.plugin-publish' version '0.21.0'
}

group 'io.github.cosmicsilence'

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(8))
    }
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'ch.epfl.scala:scalafix-interfaces:0.11.1'
    compatTestImplementation gradleTestKit()
    compatTestImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-3.0'
}

dependencyLocking {
    lockAllConfigurations()
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

stutter {
    sparse = true

    matrices {
        java8 {
            javaToolchain {
                languageVersion = JavaLanguageVersion.of(8)
            }
            gradleVersions {
                compatible '4.10'
                compatibleRange '5.1'
            }
        }
        java11 {
            javaToolchain {
                languageVersion = JavaLanguageVersion.of(11)
            }
            gradleVersions {
                compatible '4.10'
                compatibleRange '5.1'
            }
        }
    }
}

reckon {
    scopeFromProp()
    stageFromProp('beta', 'rc', 'final')
}

validatePlugins {
    failOnWarning = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

pluginBundle {
    description = 'Gradle plugin for Scalafix'
    website = 'https://github.com/cosmicsilence/gradle-scalafix'
    vcsUrl = 'https://github.com/cosmicsilence/gradle-scalafix.git'
    plugins {
        scalafixPlugin {
            id = 'io.github.cosmicsilence.scalafix'
            displayName = 'Scalafix Plugin'
            tags = ['scalafix']
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name 'BSD 3-Clause License'
            url 'https://opensource.org/licenses/BSD-3-Clause'
        }
    }
    developers {
        developer {
            name 'Marcelo Cenerino'
            email 'marcelocenerine@gmail.com'
            organization 'Cosmic Silence'
            organizationUrl 'https://github.com/cosmicsilence'
        }
        developer {
            name 'Felix Sanjuan'
            email 'felix.sanjuan@icloud.com'
            organization 'Cosmic Silence'
            organizationUrl 'https://github.com/cosmicsilence'
        }
    }

    scm {
        url 'https://github.com/cosmicsilence/gradle-scalafix'
        connection 'git@github.com:cosmicsilence/gradle-scalafix.git'
        developerConnection 'git@github.com:cosmicsilence/gradle-scalafix.git'
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cosmicsilence/gradle-scalafix")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("github.key")  ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        pluginMaven(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Plugin for Scalafix')
                root.appendNode('description', 'This is a Gradle plugin for Scalafix, a refactoring and linting tool for Scala')
                root.appendNode('url', 'https://github.com/cosmicsilence/gradle-scalafix')
                root.children().last() + pomConfig
            }
        }
    }
}
