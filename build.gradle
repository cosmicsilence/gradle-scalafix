// Fix required since jgit 6 is compiled for java 11. We can remove this lock once we move to java 11 or reckon plugins lock the version.
buildscript {
    dependencies  {
        classpath('org.eclipse.jgit:org.eclipse.jgit') {
            version {
                strictly '5.13.0.202109080827-r'
            }
        }
    }
}

plugins {
    id 'org.ajoberstar.stutter' version '0.5.1'
    id 'org.ajoberstar.reckon' version '0.12.0'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '0.11.0'
    id 'groovy'
    id 'java-gradle-plugin'
}



group 'io.github.cosmicsilence'

repositories {
    mavenCentral()
}

dependencies {
    implementation gradleApi()
    implementation localGroovy()
    implementation 'ch.epfl.scala:scalafix-interfaces:0.9.33'
    compatTestImplementation gradleTestKit()
    compatTestImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
    testImplementation 'org.spockframework:spock-core:1.3-groovy-2.5'
}

tasks.withType(Test) {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

stutter {
    sparse = true
    java(8) {
        compatibleRange '4.9', '5.0'
        compatibleRange '5.1'
    }
    java(11) {
        compatibleRange '4.9', '5.0'
        compatibleRange '5.1'
    }
}

validateTaskProperties {
    failOnWarning = true
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from javadoc.destinationDir
}

reckon {
    scopeFromProp()
    stageFromProp('beta', 'rc', 'final')
}

pluginBundle {
    description = 'Gradle plugin for Scalafix'
    website = 'https://github.com/cosmicsilence/gradle-scalafix'
    vcsUrl = 'https://github.com/cosmicsilence/gradle-scalafix.git'
    plugins {
        scalafixPlugin {
            id = 'io.github.cosmicsilence.scalafix'
            displayName = 'Scalafix Plugin'
            tags = ['scalafix']
        }
    }
}

def pomConfig = {
    licenses {
        license {
            name 'BSD 3-Clause License'
            url 'https://opensource.org/licenses/BSD-3-Clause'
        }
    }
    developers {
        developer {
            name 'Marcelo Cenerino'
            email 'marcelocenerine@gmail.com'
            organization 'Cosmic Silence'
            organizationUrl 'https://github.com/cosmicsilence'
        }
        developer {
            name 'Felix Sanjuan'
            email 'felix.sanjuan@icloud.com'
            organization 'Cosmic Silence'
            organizationUrl 'https://github.com/cosmicsilence'
        }
    }

    scm {
        url 'https://github.com/cosmicsilence/gradle-scalafix'
        connection 'git@github.com:cosmicsilence/gradle-scalafix.git'
        developerConnection 'git@github.com:cosmicsilence/gradle-scalafix.git'
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/cosmicsilence/gradle-scalafix")
            credentials {
                username = project.findProperty("github.user") ?: System.getenv("GITHUB_ACTOR")
                password = project.findProperty("github.key")  ?: System.getenv("GITHUB_TOKEN")
            }
        }
    }
    publications {
        pluginMaven(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar
            pom.withXml {
                def root = asNode()
                root.appendNode('name', 'Gradle Plugin for Scalafix')
                root.appendNode('description', 'This is a Gradle plugin for Scalafix, a refactoring and linting tool for Scala')
                root.appendNode('url', 'https://github.com/cosmicsilence/gradle-scalafix')
                root.children().last() + pomConfig
            }
        }
    }
}
